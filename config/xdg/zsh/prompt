# vim:ft=zsh:

autoload -Uz vcs_info
setopt PROMPT_SUBST

# Current time on the right
RPROMPT="%F{32}$(date +"%H:%m:%S")%f"

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:git:*' formats ' %F{220}(%F{34}%b%f%m%F{220})%f'
zstyle ':vcs_info:git:*' actionformats ' %F{220}(%F{34}%b%F{220}|%F{red}%a%f%m%F{220})%f'

# Enable hooks
zstyle ':vcs_info:git*+set-message:*' hooks git-status

# Combined hook to show all git status info
+vi-git-status() {
  local status_info=""

  # unstaged changes (modified files)
  if ! git diff --quiet 2>/dev/null; then
    status_info+="%F{red}*%f"
  fi

  # staged changes
  if ! git diff --cached --quiet 2>/dev/null; then
    status_info+="%F{34}+%f"
  fi

  # untracked files
  if git status --porcelain 2>/dev/null | grep -q '^?? '; then
    status_info+="%F{red}%%%f"
  fi

  # stash
  local stashes="$(git stash list 2>/dev/null | wc -l | tr -d ' ')"
  if [[ "${stashes}" -gt 0 ]]; then
    status_info+="%F{32}$%f"
  fi

  local upstream="$(git rev-parse --abbrev-ref --symbolic-full-name @{upstream} 2>/dev/null)"

  if [[ "${upstream}" ]]; then
    local ahead="$(git rev-list --count ${upstream}..HEAD 2>/dev/null)"
    local behind="$(git rev-list --count HEAD..${upstream} 2>/dev/null)"

    if [[ "${ahead}" -eq 0 ]] && [[ "" -eq 0 ]]; then
      status_info+=" %F{245}u=%f"
    else
      status_info+=" %F{245}u"
      [[ "${ahead}" -gt 0 ]] && status_info+="+${ahead}"
      [[ "${behind}" -gt 0 ]] && status_info+="-${behind}"
      status_info+="%f"
    fi
  fi

  # Add status info with space if there's anything to show
  if [[ -n "${status_info}" ]]; then
    # Trim left
    hook_com[misc]=" ${status_info#"${status_info%%[![:space:]]*}"}"
  fi
}

precmd() { vcs_info; }

PROMPT='
%F{32}%~%f${vcs_info_msg_0_}
%(?.%F{green}.%F{red})‚ùØ%f '
