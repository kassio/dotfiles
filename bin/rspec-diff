#!/usr/bin/env zsh

local dry=0
local list=0
local ref=""
local ignored=0
local dbchanges=0

parse_args() {
  case $1 in
    -h | --help)
      echo "Usage: rspec-diff [options] [ref]
      Options:
        -h | --help           Show this menu
        -d | --dry            Do not run the tests, only report on stats"
      exit 0
      ;;
    -d | --dry)
      dry=1
      ;;
    -l | --list)
      list=1
      ;;
    *)
      ref="$1"
      ;;
  esac

  if [ "$#" -gt 1 ]; then
    shift
    parse_args "$@"
  fi
}; parse_args "$@"

report() {
  title="$1"; shift

  if [ "${#@}" -gt 0 ]; then
    printf "%s %s\n" "${#@}" "${title}"
    for file in "${@[@]}"; do
      echo "\t- ${file}"
    done
    echo "---"
  fi
}

local files=( "${(@f)$(git-diff-names-to "${ref}" | grep '.rb')}" )
local specs=()
local modified=()
local not_modified=()
local not_found=()

[ "${FOSS_ONLY:-0}" -ne "0" ] && echo "⚠️ FOSS ONLY⚠️"

for file in "${files[@]}"; do
  if [[ "${file}" =~ "app/events/" ]] || # skip events
    [[ "${file}" =~ "spec/factories/" ]] # skip factories
  then
    continue
  fi

  # skip EE files when FOSS_ONLY is set
  if [ "${FOSS_ONLY:-0}" -ne "0" ] && [ "${file:0:2}" = "ee" ]; then
    ignored=1
    echo "» ignoring: ${file}"
    continue
  fi

  # when commit include migrations
  # - skip migration files and
  # - test the schema changes
  if [[ "${file}" =~ "db/" ]]; then
    if [ "${dbchanges}" -eq 0 ]; then
      dbchanges=1
      specs+=( "spec/db/schema_spec.rb" )
    fi
    continue
  fi

  if [[ "${file}" =~ ".*_spec.rb" ]]; then
    specs+=( "${file}" )
    modified+=( "${file}" )
  else
    possible_spec="$(echo "${file}" |
      sed 's;^app;spec;' |
      sed 's;^ee/app;ee/spec;' |
      sed 's;^lib;spec/lib;' |
      sed 's;^ee/lib;ee/spec/lib;' |
      sed 's;spec/support;spec/support_specs;' |
      sed 's;.rb;_spec.rb;')"

    if [ -f "${possible_spec}" ]; then
      if [ ! "${specs[(Ie)${possible_spec}]}" -le 0 ]; then
        specs+=( "${possible_spec}" )
      fi

      # check if possible_spec is not in files
      if [ "${files[(Ie)${possible_spec}]}" -le 0 ]; then
        not_modified+=( "${possible_spec}" )
      fi
    elif [ "${#possible_spec}" -gt 0 ]; then
      not_found+=( "${possible_spec}" )
    fi
  fi
done

if [ "${list}" -gt 0 ]; then
  for file in "${specs[@]}"; do
    echo "${file}"
  done
  exit 0
fi

[ "${ignored}" -ne "0" ] && echo ""
echo "${#files} files changed"
echo "---"
report "specs changed" "${modified[@]}"
report "specs not update" "${not_modified[@]}"
report "specs missing" "${not_found[@]}"
report "specs to run" "${specs[@]}"

if [ "${dry}" -gt 0 ]; then
  exit 0
fi

if [ "${#specs}" -gt 0 ]; then
  bundle exec spring stop 2>/dev/null
  bundle exec rspec "${specs[@]}"
else
  echo "no rspecs to run"
fi
