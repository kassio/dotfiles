#!/usr/bin/env zsh

local ref=""
if [ "$#" -gt 0 ]; then
  ref="$1"
  shift
fi

report() {
  title="$1"; shift

  printf "%s %s\n" "${#@[@]}" "$title"
  for file in "${@[@]}"; do
    echo "\t- ${file}"
  done
  echo "---"
}

local files=( "${(@f)$(git-diff-names-to "${ref}")}" )
local specs=()
local modified=()
local not_modified=()
local not_found=()

for file in "${files[@]}"; do
  if [[ "${file}" =~ ".*_spec.rb" ]]; then
    specs+=( "${file}" )
    modified+=( "${file}" )
  elif [[ ! "${file}" =~ ".*spec/.*" ]]; then
    possible_spec="$(echo "${file}" |
      sed 's;^app;spec;' |
      sed 's;^ee/app;ee/spec;' |
      sed 's;^lib;spec/lib;' |
      sed 's;^ee/lib;ee/spec/lib;' |
      sed 's;.rb;_spec.rb;')"

    if [ -f "${possible_spec}" ]; then
      specs+=( "${possible_spec}" )

      if [ "${files[(Ie)${possible_spec}]}" -le 0 ]; then
        not_modified+=( "${possible_spec}" )
      fi
    else
      not_found+=( "${possible_spec}" )
    fi
  fi
done

report "specs changed" "${modified[@]}"
report "specs not update" "${not_modified[@]}"
report "specs missing" "${not_found[@]}"

bundle exec rspec "$@" "${specs[@]}"
