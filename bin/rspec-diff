#!/usr/bin/env zsh

local dry=0
local ref=""

parse_args() {
  case $1 in
    -h | --help)
      echo "Usage: rspec-diff [options] [ref]
      Options:
        -h | --help           Show this menu
        -d | --dry            Do not run the tests, only report on stats"
      exit 0
      ;;
    -d | --dry)
      dry=1
      ;;
    *)
      ref="$1"
      ;;
  esac

  if [ "$#" -gt 1 ]; then
    shift
    parse_args "$@"
  fi
}; parse_args "$@"

report() {
  title="$1"; shift

  printf "%s %s\n" "${#@}" "${title}"
  for file in "${@[@]}"; do
    echo "\t- ${file}"
  done
  echo "---"
}

local files=(
  "${(@f)$(git-diff-names-to "${ref}" |
    grep -ve '^db/' | # skip migration
    grep '.rb')}" )
local specs=()
local modified=()
local not_modified=()
local not_found=()

for file in "${files[@]}"; do
  if [[ "${file}" =~ ".*_spec.rb" ]]; then
    specs+=( "${file}" )
    modified+=( "${file}" )
  else
    possible_spec="$(echo "${file}" |
      sed 's;^app;spec;' |
      sed 's;^ee/app;ee/spec;' |
      sed 's;^lib;spec/lib;' |
      sed 's;^ee/lib;ee/spec/lib;' |
      sed 's;spec/support;spec/support_specs;' |
      sed 's;.rb;_spec.rb;')"

    if [ -f "${possible_spec}" ]; then
      specs+=( "${possible_spec}" )

      if [ "${files[(Ie)${possible_spec}]}" -le 0 ]; then
        not_modified+=( "${possible_spec}" )
      fi
    else
      not_found+=( "${possible_spec}" )
    fi
  fi
done

echo "${#files} files changed"
echo "---"
report "specs changed" "${modified[@]}"
report "specs not update" "${not_modified[@]}"
report "specs missing" "${not_found[@]}"

if [ "${dry}" -gt 0 ]; then
  exit 0
fi

bundle exec rspec "${specs[@]}"
